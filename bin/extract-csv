#!/usr/bin/env perl
use 5.16.0;
use warnings;
use utf8::all;
use Anki::Database;
use Text::CSV_XS;
use DateTime;

@ARGV >= 2 or die "usage: $0 model [field / _created / _created_unix / _suspended | _tags]...\n";
my $model = shift;
my $suspended = 0;
my @fields;

for (@ARGV) {
  if ($_ eq '_suspended') {
    $suspended = 1;
  } else {
    push @fields, $_;
  }
}

my $anki = Anki::Database->new;
my $csv = Text::CSV_XS->new({binary => 1, auto_diag => 1});

$anki->each_card(sub {
    my ($card) = @_;

    if ($suspended) {
      return unless $card->suspended;
    }

    my @values = map { $_ eq '_created_unix' ? int($card->id/1000) : $_ eq '_created' ? DateTime->from_epoch(epoch => $card->id/1000)->ymd : $_ eq '_tags' ? $card->tags_as_string : $card->field($_) } @fields;
    $csv->say(\*STDOUT, \@values);
}, $model);

