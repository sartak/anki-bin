#!/usr/bin/env perl
use 5.14.0;
use warnings;
use utf8::all;
use Anki::Morphology;
use Getopt::Whatever;

my $morph  = Anki::Morphology->new;
my $anki   = $morph->anki;
my $corpus = $morph->corpus;

my %i = map { $_ => 1 } $morph->known_morphemes;
say "You know " . keys(%i) . " words!";

if ($ARGV{add}) {
    $i{$_}++ for ref($ARGV{add}) ? @{ $ARGV{add} } : $ARGV{add};
}

my %anti_i;
my %source_count;
my %near_miss;
my @i_plus;

open my $known_sentences, '>', 'known.txt';

my $prev_source = '';
my ($i, $e) = (0, 0);

sub process {
    my ($sentence, $source, $fid, $morphemes) = @_;
    say 2**$e++ if ++$i == 2**$e;

    my (@known, @unknown);
    for my $dict (defined($morphemes) ? split ' ', $morphemes : map { $_->{dictionary} } $morph->morphemes_of($sentence)) {
        if ($i{$dict}) {
            push @known, $dict;
        }
        else {
            push @unknown, $dict;
            $anti_i{$dict}++ if !$ARGV{'verbose'};
        }
        $anti_i{$dict}++ if $ARGV{'verbose'};
    }

    if (@unknown == 1) {
        $near_miss{ $unknown[0] }++;
    }

    if ($source ne $prev_source) {
        print $known_sentences "\n" if $prev_source ne '';
        print $known_sentences "$source\n";
        $prev_source = $source;
    }

    push @{ $i_plus[ @unknown ] }, $sentence;

    if (@unknown == 0) {
        print $known_sentences "  ";
        print $known_sentences "$fid|" if $fid;
        print $known_sentences "$sentence\n";
        $source_count{$source}{known}++;
    }
    else {
        $source_count{$source}{unknown}++;
    }
}

unless ($ARGV{'skip-corpus'}) {
    my $order = $corpus->order;

    my @extra;
    push @extra, "AND source NOT IN ('吾輩は猫である', 'こゝろ', '坊っちゃん')" if $ARGV{'skip-classics'};
    push @extra, "AND source NOT IN ('Twitter')" if $ARGV{'skip-twitter'};
    push @extra, "AND source NOT IN ('MFSP', 'Smart.fm', 'ARES-3', 'プログレッシブ英和・和英中辞典', '四字熟語', '四字熟語 Example', 'Twitter')" if $ARGV{'skip-nonbooks'};
    push @extra, "AND source NOT LIKE 'ハリー・ポッターと%' AND source NOT IN ('吾輩は猫である', 'こゝろ', '坊っちゃん')" if $ARGV{'skip-books'};
    my $extra = join "\n", @extra;

    my $sth = $corpus->prepare("
        SELECT japanese, source, NULL, morphemes FROM sentences
        WHERE suspended = 1
        $extra
        ORDER BY $order
    ;");
    $sth->execute;
    while (my ($sentence, $source, $fid, $morphemes) = $sth->fetchrow_array) {
        process($sentence, $source, $fid, $morphemes);
    }
}

unless ($ARGV{'skip-anki'}) {
    $anki->each_card(sub {
        my ($card) = @_;
        return if $card->model->name ne '文';
        return if !$card->suspended;

        process($card->field('日本語'), 'Anki', $card->note_id, undef);
    });
}

if ($ARGV{'from-file'}) {
    open my $handle, '<', $ARGV{'from-file'}
        or die "unable to open $ARGV{'from-file'} for reading: $!";
    while (<$handle>) {
        for (/[「」【】『』〈〉]/ ? $_ : split /。/, $_) {
            next unless /[\p{Unified_Ideograph}\p{Hiragana}\p{Katakana}]/;

            chomp;
            s/^\s+//;
            s/\s+$//;

            process($_, $ARGV{'from-file'}, undef, undef);
        }
    }
}

open my $freq, '>', 'freq.txt';
for my $word (sort { $anti_i{$b} <=> $anti_i{$a} } keys %anti_i) {
    print $freq "# " if $ARGV{'verbose'} && $i{$word};
    print $freq "$word: $anti_i{$word}\n";
}

open my $ip, '>', 'i-plus.txt';
for (my $i = 0; $i < @i_plus; ++$i) {
    next if !$i_plus[$i];
    print $ip "i+$i (".@{$i_plus[$i]}."):\n";
    print $ip "  $_\n" for @{ $i_plus[$i] };
    print $ip "\n";
}

open my $near_miss, '>', 'near-miss.txt';
for my $word (sort { $near_miss{$b} <=> $near_miss{$a} } keys %near_miss) {
    print $near_miss "$word: $near_miss{$word}\n";
}

for my $source (sort keys %source_count) {
    my $known = $source_count{$source}{known};
    my $unknown = $source_count{$source}{unknown};
    my $ratio = 100 * $known / ($known+$unknown);
    say sprintf '%3d%% (K:%d U:%d) %s', $ratio, $known, $unknown, $source;
}
